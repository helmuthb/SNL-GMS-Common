buildscript {
    repositories {
        maven {
            url "${artifactory_contextUrl}/sandbox_java_virtual"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.3.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6"
    }
}

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    group 'gms.shared.mechanisms.objectstoragedistribution'
}

artifactoryPublish.skip = true

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'
    sourceCompatibility = 9

    dependencies {
        //testing
        testImplementation 'junit:junit:4.12'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

configure([project(':signaldetection-repository-service'), project(':waveforms-repository-service')]) {
    artifactoryPublish.skip = true
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    //The base Artifactory URL if not overridden by the publisher/resolver

    publish {
        repository {
            repoKey = 'sandbox_java_local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'sandbox_java_virtual'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
